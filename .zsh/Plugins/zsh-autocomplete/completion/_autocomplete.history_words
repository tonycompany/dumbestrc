#autoload

local tag=history-words
_tags $tag
_tags && _requested $tag ||
  return 1

local -a line_words disp=() matches=()
local -i index
local lbuffer rbuffer line word
local sublist_delim=$'[|&!;\n<>{}]'

index=$LBUFFER[(I)$~sublist_delim]
if (( index <= $#LBUFFER )); then
  lbuffer="${(j:*:)${(Z+C+@)LBUFFER[index+1,-1]}}"
fi
index=$RBUFFER[(i)$~sublist_delim]
if (( index <= $#RBUFFER )); then
  rbuffer="${(j:*:)${(Z+C+@)RBUFFER[1,index-1]}}"
fi

for line in ${(@)history[(R)*$~lbuffer*$~rbuffer*]}; do
  line_words=( "${(Z+C+@)line}" )
  line_words=( ${(@)line_words[$line_words[(ie)$words[1]]+1,-1]} )
  line_words=( ${(@)line_words[1,$line_words[(ie)$~sublist_delim]-1]} )

  for word in $line_words[@]; do
    case $word in
      \$\'*)
        word=${(QQQQ)word}
        ;;
      \"*)
        word=${(QQQ)word}
        ;;
      \'*)
        word=${(QQ)word}
        ;;
      *)
        word=${(Q)word}
        ;;
    esac
    matches+=( "$word" )
    done
done

local expl
_description $tag expl 'history word'
compadd "$expl[@]" -fQ -a matches
